  cal entry
  hlt
; names
addname: 43 0
buf: 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0
msg: 65 68 68 73 78 71 33 33 10 0 0 0 0 0 0 0
entry:
again:
  lad buf     ; bl bh
  cal getnext

  lad buf  ; bl bh
  fch      ; c
  bnz test
  ret

test:
  lad buf     ; bl bh
  lad addname ; bl bh al ah
  cal strcmp  ; df
  bnz echo

  lad msg     ; ml mh
  cal puts

  lit 1
  bnz echo

echo:
  lad buf  ; bl bh
  cal puts
  
  lit 10    ; \n
  cal putc
  
  lit 1     ; 1
  bnz again ; 1

strcmp:
  nop  ; bl bh al ah
loop4:
  ovr      ; bl bh al ah al
  ovr      ; bl bh al ah al ah
  psh      ; bl bh al ah
  fch      ; bl bh c
  rot      ; bh c bl
  rot      ; c bl bh
  ovr      ; c bl bh bl
  ovr      ; c bl bh bl bh
  psh      ; c bl bh
  fch      ; c d
  ovr      ; c d c
  ovr      ; c d c d
  sub      ; c d c-d
  dup      ; c d df df
  bnz diff ; c d df
  rot      ; d df c
  bnz gc   ; d df
  ovr      ; d df d
  psh      ; d
  drp
  pop      ; df d
  drp      ; df
  lit 1    ; df 1
  bnz diff
gc:
  ovr       ; d df d
  psh       ; d
  drp
  pop       ; df d
  bnz loop4 ; df

diff:
  nop ; df
  ret

getnext:
loop1:
  cal getc   ; bl bh c
  dup        ; bl bh c c
  bng eof    ; bl bh c
  dup        ; bl bh c c
  lit 32     ; bl bh c c 32
  sub        ; bl bh c c-32
  ovr        ; bl bh c c-32 c
  lit 10     ; bl bh c c-32 c 10
  sub        ; bl bh c c-32 c-10
  and        ; bl bh c (c-32)&(c-10)
  bnz captr  ; bl bh c
  drp        ; bl bh
  lit 1      ; bl bh 1
  bnz loop1  ; bl bh

captr:
  dup         ; bl bh c c
  bng eof     ; bl bh c
  lit 0       ; bl bh c 0
  psh         ; bl bh
  ovr         ; bl bh bl
  ovr         ; bl bh bl bh
  pop         ; bl bh bl bh c 0
  drp         ; bl bh bl bh c
  rot         ; bl bh bh c bl
  rot         ; bl bh c bl bh
  put         ; bl bh 
  lit 1       ; bl bh  1
  rot         ; bh 1 bl
  add         ; bh 1+bl
  ovr         ; bh bl' bh
  psh         ; bh
  drp
  pop         ; bl' bh
  cal getc    ; bl' bh c
  dup         ; bl' bh c c
  lit 32      ; bl' bh c c 32
  sub         ; bl' bh c c-32
  ovr         ; bl' bh c c-32 c
  lit 10      ; bl' bh c c-32 c 10
  sub         ; bl' bh c c-32 c-10
  and         ; bl' bh c (c-32)&(c-10)
  bnz captr   ; bl' bh c

eof:
  drp   ; bl' bh
  lit 0 ; bl' bh 0
  rot   ; bh 0 bl'
  rot   ; 0 bl' bh
  put
  ret

puts:
loop3:
  ovr       ; bl bh bl
  ovr       ; bl bh bl bh
  fch       ; bl bh c
  dup       ; bl bh c c
  bnz !null ; bl bh c

  drp      ; bl bh
  drp      ; bl
  drp
  ret

!null:
  cal putc  ; bl bh
  lit 1     ; bl bh 1
  rot       ; bh 1 bl
  add       ; bh 1+bl
  ovr       ; bh bl' bh
  psh       ; bh
  drp
  pop       ; bl' bh
  lit 1     ; bl' bh 1
  bnz loop3 ; bl' bh


PORT_IN= 65021
PORT_OUT= 65022
PORT_DATA= 65023


getc:
  lad PORT_IN ; PORT_IN
  fch         ; 0
  drp
  lad PORT_DATA ; PORT_DATA
  fch           ; c
  ret

putc:
  lad PORT_DATA ; c PORT_DATA
  put
  lad PORT_OUT ; PORT_OUT
  fch          ; 0
  drp
  ret
